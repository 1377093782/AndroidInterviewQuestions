apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "24.0.3"

    defaultConfig {
        applicationId "com.wuxl.interviewquestions"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 7
        versionName "2.4"
        multiDexEnabled true; //突破应用方法数65535的限制
        manifestPlaceholders = [UMENG_CHANNEL_CALUE: "umeng"]
        vectorDrawables.useSupportLibrary = true
    }

    //添加签名文件配置
    signingConfigs {
        debugConfig {}

        releaseConfig {
            storeFile file(RELEASE_STORE_FILE)
            keyAlias RELEASE_KEY_ALIAS
            def sPassword = null
            def kPassword = null
            if (System.console() != null)
                sPassword = System.console().readLine("\\\\nEnter store password ")
            if (System.console() != null)
                kPassword = System.console().readLine("\\\\nEnter key password ")
            storePassword sPassword
            keyPassword kPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig

            //指定release包输出的文件名就是渠道的名字
            applicationVariants.all {
                variant ->
                    variant.outputs.each {
                        output ->
                            def outputFile = output.outputFile
                            if (outputFile != null && outputFile.name.endsWith(".apk")) {
                                def fileName = "${variant.productFlavors[0].name}" + ".apk"
                                output.outputFile = new File(outputFile.parent, fileName);
                            }
                    }
            }
        }
    }

    //多渠道支持
    productFlavors {
        xiaomi {

        }

//        baidu {
//
//        }

    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_CALUE: name]
    }

    //兼容Android6.0系统所需
    useLibrary 'org.apache.http.legacy'
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:25.1.1'
    //Bmob的android sdk包，包含了Bmob的数据存储、文件等服务
    compile 'cn.bmob.android:bmob-sdk:3.5.0'
    //如果你想应用能够兼容Android6.0，请添加此依赖(org.apache.http.legacy.jar)
    compile 'cn.bmob.android:http-legacy:1.0'
    //刷新和加载更多pullloadmorerecyclerview
    compile 'com.wuxiaolong.pullloadmorerecyclerview:library:1.1.1'
    compile 'com.android.support:cardview-v7:25.1.1'
    //ButterKnife
    compile 'com.jakewharton:butterknife:7.0.0'
    //Bugly，其中latest.release指代最新Bugly SDK版本号，也可以指定明确的版本号，例如2.2.0
    compile 'com.tencent.bugly:crashreport:latest.release'
    compile 'com.android.support:design:25.1.1'
    //友盟
    compile 'com.umeng.analytics:analytics:latest.integration'
}
